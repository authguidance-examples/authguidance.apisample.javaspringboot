plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'oauth.apisample.javaspringboot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    // API dependency versions
    spring_boot_starter_version = '2.6.6'
    spring_security_version = '5.6.2'
    spring_beans_version = '5.3.19'
    jose4j_version = '0.7.11'
    cache2k_version = '1.6.0.Final'
    commons_codec_version = '1.15'
    jackson_version = '2.13.2'
    apache_http_client_version = '4.5.13'
    async_file_version = '1.1.2'
    logback_version = '7.0.1'
    lombok_version = '1.18.22'
    guava_version = '31.1-jre'
    gethostname4j_version = '1.0.0'

    // Test dependency versions
    junit_jupiter_version = '5.8.2'
    junit_platform_suite_version = '1.8.2'
    wiremock_version = '2.27.2'
}

dependencies {

    // Spring dependencies
    implementation "org.springframework.boot:spring-boot-starter-web:$spring_boot_starter_version"
    implementation "org.springframework.security:spring-security-config:$spring_security_version"
    implementation "org.springframework.security:spring-security-web:$spring_security_version"
    implementation "org.springframework:spring-beans:$spring_beans_version"

    // OAuth and caching dependencies
    implementation "org.bitbucket.b_c:jose4j:$jose4j_version"
    implementation "org.cache2k:cache2k-base-bom:$cache2k_version"
    implementation "commons-codec:commons-codec:$commons_codec_version"

    // Jackson dependencies
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version.2"

    // Utilities
    implementation "org.apache.httpcomponents:httpclient:$apache_http_client_version"
    implementation "com.github.javasync:AsyncFileRw:$async_file_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logback_version"
    implementation "com.google.guava:guava:$guava_version"
    implementation "com.kstruct:gethostname4j:$gethostname4j_version"

    // Compiler dependencies
    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testImplementation "org.junit.platform:junit-platform-suite-engine:$junit_platform_suite_version"
    testImplementation "com.github.tomakehurst:wiremock:$wiremock_version"

    // Test compiler dependencies
    testCompileOnly "org.projectlombok:lombok:$lombok_version"
    testAnnotationProcessor "org.projectlombok:lombok:$lombok_version"
}

tasks.named('test') {
    useJUnitPlatform()
}